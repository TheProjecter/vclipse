grammar org.vclipse.vcml.Constraint with org.eclipse.xtext.common.Terminals

import "platform:/resource/org.vclipse.vcml.mm/src/org/vclipse/vcml/mm/VCML.ecore"
import "http://www.eclipse.org/emf/2002/Ecore" as ecore

ConstraintSource hidden (WS, DEPENDENCY_COMMENT)
	:   'objects' ':'	objects+=ConstraintObject (',' objects+=ConstraintObject)* '.'?

		('condition' ':' condition=Condition_C '.'?)?

		('restriction' | 'restrictions') ':'
		restrictions+=ConditionalConstraintRestriction (',' restrictions+=ConditionalConstraintRestriction)*
		'.'?

		('inferences' ':' inferences+=CharacteristicReference_C (',' inferences+=CharacteristicReference_C)* '.'?)?
    ;

ConstraintObject
	:	ConstraintClass
	|	ConstraintMaterial 
	;
	
ConstraintClass
	:	(name=SHORTVAR 'is_a')? class=[Class|CLASSNAME] 
		(	'where'
			shortVars+=ShortVarDefinition (';' shortVars+=ShortVarDefinition)*
		)?
	;

ConstraintMaterial
	:	(name=SHORTVAR 'is_object')? objectType=ObjectType
		(	'where'
			shortVars+=ShortVarDefinition (';' shortVars+=ShortVarDefinition)*
		)?
	;

ObjectType
	:	'(' type='material' ')' '(' classType=INT ')' '(' attrs+=PartialKey (',' attrs+=PartialKey)* ')'
	;

 // TODO how to handle cross references?
PartialKey
	:	name='nr' '=' value=SYMBOL
	;

ShortVarDefinition
	:	name=SHORTVAR '=' characteristic=[Characteristic | XID]
	;

PartOfCondition :
	'part_of' '(' child=[ConstraintObject | SHORTVAR] ',' parent=[ConstraintObject | SHORTVAR] ')'
	; 
SubpartOfCondition :
	'subpart_of' '(' child=[ConstraintObject | SHORTVAR] ',' parent=[ConstraintObject | SHORTVAR] ')'
	; 

ConditionalConstraintRestriction returns ConstraintRestriction :
	ConstraintRestrictionLHS ({ConditionalConstraintRestriction.restriction=current} 'if' condition=Condition_C)?
	;

ConstraintRestrictionFalse
	:	{ConstraintRestrictionFalse}
	   'false'
	;

ConstraintRestrictionLHS returns ConstraintRestriction
	:	InCondition_C
	|	IsSpecified_C
	|	PartOfCondition
	|	SubpartOfCondition
	|	Comparison_C
	|	Table_C
	|	Function_C
	|	NegatedConstraintRestrictionLHS
	|	ConstraintRestrictionFalse
	;
	
NegatedConstraintRestrictionLHS
	:	'not' restriction=(Table_C | Function_C)
	;

/*
EqualsRestriction :
	characteristic=CharacteristicReference_C '=' expression=Expression_C
	;
*/

CharacteristicReference_C
	:	ObjectCharacteristicReference
	|	ShortVarReference
	;
	
ObjectCharacteristicReference
	:	location=[ConstraintObject | SHORTVAR] '.' characteristic=[Characteristic|XID]
	;

ShortVarReference
	:	ref=[ShortVarDefinition | SHORTVAR]
	;

Function_C returns Function
	:	'function' function=[VariantFunction|XID]
		'('
			characteristics+=[Characteristic|XID] '=' values+=Literal_C
			(',' characteristics+=[Characteristic|XID] '=' values+=Literal_C)*
		')'
	;

Table_C returns Table
	:	'table' table=[VariantTable|XID]
		'('
			characteristics+=[Characteristic|XID] '=' values+=Literal_C
			(',' characteristics+=[Characteristic|XID] '=' values+=Literal_C)*
		')'
	;
	
///////////////////////////////////////////////////////////////////////////////
// Expressions
///////////////////////////////////////////////////////////////////////////////

Expression_C returns Expression
    :	MultiplicativeExpression_C ({BinaryExpression.left=current} operator=("+" | '-') right=MultiplicativeExpression_C)*
	;
	
MultiplicativeExpression_C returns Expression
	:	UnaryPrimaryExpression_C ({BinaryExpression.left=current} operator=('*' | '/' | '||') right=UnaryPrimaryExpression_C)*
    ;

UnaryPrimaryExpression_C returns Expression
	:	UnaryExpression_C
	|	PrimaryExpression_C
	;

UnaryExpression_C returns UnaryExpression
	:	operator=UnaryExpressionOperator expression=UnaryPrimaryExpression_C
	;
	
PrimaryExpression_C returns Expression
	:	'(' Expression_C ')'
	|	Literal_C
	|	FunctionCall_C
	;	

Literal_C returns Literal
	:	CharacteristicReference_C
	| 	MDataCharacteristic_C
	|	NumericLiteral
	|	SymbolicLiteral
	;

FunctionCall_C returns FunctionCall
	:	function=FunctionName '(' argument=Expression_C ')'
	;

MDataCharacteristic_C
	:	'mdata' characteristic=CharacteristicReference_C
	;

NumericLiteral :
	value=NUMBER
	;

SymbolicLiteral :
	value=SYMBOL
	;

///////////////////////////////////////////////////////////////////////////////
// Conditions
///////////////////////////////////////////////////////////////////////////////

Condition_C returns Condition
	:	Conjunction_C ({BinaryCondition.left=current} operator='or' right=Conjunction_C)* 
	; 

Conjunction_C returns Condition
	:	Negation_C ({BinaryCondition.left=current} operator='and' right=Negation_C)*
	; 

Negation_C returns Condition
	:	UnaryCondition_C
	|	PrimaryCondition_C
	;

UnaryCondition_C returns UnaryCondition
	:	'not' condition=Negation_C // not may be nested in constraints
	;

// TODO not must not be used around specified, part_of and subpart_of - put into validation

PrimaryCondition_C returns Condition
	:	Comparison_C
	|	'(' Condition_C ')'
 	|	InCondition_C
 	|	IsSpecified_C
 	|	PartOfCondition
	|	SubpartOfCondition 
	;

Comparison_C returns Comparison
	:	left=Expression_C operator=ComparisonOperator right=Expression_C
	;

IsSpecified_C
	:	characteristic=CharacteristicReference_C 'specified'
	|	'specified' characteristic=CharacteristicReference_C
    ;

InCondition_C
	:	characteristic=CharacteristicReference_C 'in' list=List
	;

List :
		NumberList
	|	SymbolList
	;

NumberList :
	'(' entries+=NumberListEntry (',' entries+=NumberListEntry)* ')'
	;

NumberListEntry :
	  NumericLiteral
	| NumericInterval
	;
	
NumberListEntryForValues returns NumberListEntry :
	  NumericLiteral
	| '(' NumericInterval ')'
	;
	
NumericInterval :
	lowerBound=NUMBER '-' upperBound=NUMBER
	;

SymbolList :
	'(' entries+=SymbolicLiteral (',' entries+=SymbolicLiteral)* ')'
	;

enum UnaryExpressionOperator :
	PLUS  = '+' | 
	MINUS = '-' | 
	LC = 'lc' |
	UC = 'uc'
	;

enum ComparisonOperator : 
	EQ = '='  | EQ = 'eq' |
	NE = '<>' | NE = 'ne' |
	GT = '>'  | GT = 'gt' |
    GE = '>=' | GE = '=>' | GE = 'ge' |
	LT = '<'  | LT = 'lt' |
	LE = '<=' | LE = '=<' | LE = 'le'
	;
	
enum FunctionName :
		SIN    = 'sin'
	|	COS    = 'cos'
	|	TAN    = 'tan'
	|	EXP    = 'exp'
	|	LN     = 'ln'
	|	ABS    = 'abs'
	|	SQRT   = 'sqrt'
	|	LOG10  = 'log10'
	|	ARCSIN = 'arcsin'
	|	ARCCOS = 'arccos'
	|	ARCTAN = 'arctan'
	|	SIGN   = 'sign'
	|	FRAC   = 'frac'
	|	CEIL   = 'ceil'
	|	TRUNK  = 'trunk'
	|	FLOOR  = 'floor'
	;

terminal ID	// Xtext standard without ^ 
	:	('a'..'z'|'A'..'Z'|'_') ('a'..'z'|'A'..'Z'|'_'|'0'..'9')*
	;

terminal DEPENDENCY_COMMENT
	:	(' '|'\t')* ('\r'? '\n*') !('\n'|'\r')*
	;

terminal SYMBOL // enclosed in single quotes, no escapes, no line breaks, no tabs
	:	"'" ( !("'"|'\t'|'\n'|'\r') )* "'"
	; 

XID
	:	ID
	|	'AF'
	|	'AR'
	|	'BG'
	|	'CA'
	|	'CS'
	|	'DA'
	|	'DE'
	|	'EL'
	|	'EN'
	|	'ES'
	|	'ET'
	|	'FI'
	|	'FR'
	|	'HE'
	|	'HR'
	|	'HU'
	|	'ID'
	|	'IS'
	|	'IT'
	|	'JA'
	|	'KO'
	|	'LT'
	|	'LV'
	|	'MS'
	|	'NL'
	|	'NO'
	|	'PL'
	|	'PT'
	|	'RO' 
	|	'RU'
	|	'SH'
	|	'SK'
	|	'SL'
	|	'SR'
	|	'SV'
	|	'TH'
	|	'TR'
	|	'UK'
	|	'Z1'
	|	'ZF'
	|	'ZH'
	|	'e'
	;

EXTENDED_ID
	: XID
	| SYMBOL
	;
//EXTENDED_ID hidden () // no whitespace might be used inside extended ids
//	:	(INT | ID | '/' | ':' | '-' | '.')+ 
//	;	

// bug has been filed as Xtext bug https://bugs.eclipse.org/bugs/show_bug.cgi?id=303212
SHORTVAR // NOTE: due to a bug in Xtext, the following does not work: hidden () // no whitespace might be used inside shortvars // TODO verify this in SAP
	:	('?' | '#')? XID 
	;	

// TODO implement size restrictions for a number: e.g., the exponent's maximum value is 99
NUMBER hidden ()
	:	('-'|'+')? INT ('.' INT)? ('e' ('-'|'+') INT)? // sign of exponent is mandatory in SAP
	;

/*
TODO:
 * handle characteristics with hyphens in their name. Use in dependencies code as: skey 'cstic-name-with-hyphens'
 * handle keywords 'INVISIBILITY', 'INV', 'SKEY'
 * handle intervals with "bound operators", e.g., >1 - <5
*/

CLASSNAME
	:	'(' INT ')' XID
	;

